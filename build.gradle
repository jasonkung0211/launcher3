buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

def gitSha() {
  def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = 'git log -n 1 --format=%at'.execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}



apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.jasonkung.launcher3"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 19
        versionName "1.18"
        vectorDrawables.useSupportLibrary = true

        testApplicationId "com.jasonkung.launcher3.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //resConfigs "en", "de", "fr", "it"
        //resConfigs "xhdpi", "xxhdpi"
    }
    signingConfigs {
        release {
            storeFile file("********.jks")
            storePassword "*********"
            keyAlias "********"
            keyPassword "********"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            shrinkResources true // 是否去除無效的資源文件
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res', 'WallpaperPicker/res']
            java.srcDirs = ['src', 'WallpaperPicker/src']
            manifest.srcFile 'AndroidManifest.xml'
            //proto.srcDirs 'protos/'
        }

        androidTest {
            java.srcDirs = ['tests/src']
            res.srcDirs = ['tests/res']
            manifest.srcFile "tests/AndroidManifest.xml"
        }
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'MissingTranslation'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

repositories {
    mavenCentral()
    google()
    flatDir {
        dirs 'libs'// for aar libs
    }
    maven {
        url "https://jitpack.io"
    }
}

configurations.all {
  resolutionStrategy {
    force 'com.android.support:support-annotations:23.1.1'
  }
}

dependencies {

    //JSR-310
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

    //butter knife
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    compile 'com.android.support:support-compat:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    //compile 'com.android.support:support-v4:25.3.1'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

    //
    compile 'com.github.javiersantos:PiracyChecker:1.0.2'
    compile 'com.melnykov:floatingactionbutton:1.3.0'

    //Material Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.4.3'

    //Material Menu
    //compile 'com.balysv.materialmenu:material-menu:2.0.0'
}
